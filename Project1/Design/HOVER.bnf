<program> ::= <stmt_list>
<stmt_list> ::= <stmt>; | <stmt>; <stmt_list> | <if_stmt>; <stmt_list> | <comment><stmt_list>
<stmt> ::= <declare> | <assign> | <declare_assign> | <expr> | <loop> | <update_stmt> | <func_call> | <function_declare>  | <IO_stmt> | <primitive_func> 
<function_declare> ::= <non_void_funtion_declare> | <void_function_declare>
<non_void_funtion_declare> ::= <type> FUNC <id>(<parameters>){<stmt_list><return_stmt>;} | <type> FUNC <id>(){<stmt_list><return_stmt>;} 
<void_function_declare> ::= VOID FUNC <id>(<parameters>){<stmt_list>} | VOID FUNC <id>(){<stmt_list>}
<parameters> ::= <parameter> | <parameter> , <parameters>
<parameter> ::= <type> <id> | <type> <id> = <literal>
<return_stmt> ::= RETURN <single_return> | RETURN <void_return>
<single_return> ::= <expr> | <func_call> | <return_array>
<return_array> ::= [<return_array_exp>]
<return_array_exp> ::= <id> | <literal_without_array> | <id>,<return_array_exp> | <literal_without_array>,<return_array_exp>
<void_return> ::= VOID
<expr> ::= <id> | <literal> | <arithmetic_expr> | <logical_expr> | <array_access>
<literal_without_array> ::= <signed_int> | <string> | <char> | <float> | <long> | <double> | NULL
<literal> ::= <literal_without_array> | <array> | <multi_array>
<arithmetic_expr> ::= <arithmetic_expr> + <term> | <arithmetic_expr> - <term> | <term>
<term> ::= <term> * <sub_term> | <term> / <sub_term> | <sub_term>
<sub_term> ::= <term> ** <factor> | <term> % <factor> | <factor>
<factor> ::= ( <arithmetic_expr> ) | <id> | <literal>
<logical_expr> ::= <logical_expr> OR <logic_term> | <logic_term>
<logic_term> ::= <logic_term> AND <logic_factor> | <logic_factor>
<logic_factor> ::= (logical_expr) | !<logical_expr> | <id> | <literal>
<declare> ::= <non_array_declare> | <array_declare>
<non_array_declare> ::= <type><id_list>
<id_list> ::= <id> | <id>,<id_list>
<array_declare> ::= <simple_array_declare> | <multi_array_declare>
<simple_array_declare> ::= <type>[]<id>
<multi_array_declare> ::= <type><dimension><id>
<dimension> ::= [][] | []<dimension>
<assign> ::= <non_array_assign> | <array_assign>
<non_array_assign> ::= <id><assignment_op><expr> | <id><assignment_op><func_call> 
<array_assign> ::= <single_array_assign> | <multi_array_assign>
<single_array_assign> ::= <id>[<signed_int>]<assignment_op><expr> | <id><assignment_op><func_call> 
<multi_array_assign> ::= <id><indexed_dimension><assignment_op><expr> | <id><assignment_op><func_call> 
<indexed_dimension> ::= [<signed_int>][<signed_int>] | [<signed_int>]<indexed_dimension>
<declare_assign> ::=  <constant_declare_assign> | <type><id> = <expr> | <simple_array_declare> = [<list>] | <multi_array_declare_assign>
<constant_declare_assign> ::= CONSTANT <type><id> = <literal> 
<multi_array_declare_assign> ::= <multi_array_declare> = <multi_list>
<multi_list> ::= [<list>][<list>] | [<list>]<multi_list>
<list> ::= <literal> | <id> | <literal>,<list> | <id>,<list>
<multi_array> ::= <array><array> | <array><multi_array>
<array> ::= [<array_exp>]
<array_exp> ::= <literal_without_array> | <literal_without_array>,<array_exp>
<if_stmt> ::= <matched> | <unmatched>
<matched> ::= IF (<conditional_expr>) {<matched>} ELSE {<matched>} | <non_if_stmt>
<non_if_stmt> ::= <stmt>;<non_if_stmt> | <stmt>;
<unmatched> ::= IF (<conditional_expr>) {<stmt_list>} | IF (<conditional_expr>) {<matched>} ELSE {<unmatched>}
<conditional_expr> ::= <id> | <relational_exp> | <range_condition> | <array_access>
<range_condition> ::= <id><range_keyword><range_bound>,<range_bound> | <literal><range_keyword><range_bound>,<range_bound>
<range_bound> ::= <float> | <signed_int> | <id>
<relational_exp> ::= <expr><relational_op><expr>
<func_call> ::= <id>(<parameters>)
<range_keyword> ::= WITHIN | OUTSIDE
<array_access> ::= <id>[<signed_int>] | <id><multi_array_access>
<multi_array_access> ::= [<signed_int>][<signed_int>] | <multi_array_access>[<signed_int>]
<comment> ::= #<string># 
<loop> ::= <for_loop> | <while_loop> | <dowhile_loop> | <times_loop>
<for_loop> ::= for (<for_init>;<for_condition>;<for_update>) {<stmt_list>}
<for_init> ::=  <for_init>, <assign> | <for_init>, <declare> | <for_init>, <declare_assign> | <declare_assign> | <assign> | <declare>
<for_condition> ::= <conditional_expr>
<for_update> ::= <assignment> | <update_expr>
<while_loop> ::= while (<expr>){<stmt_list>}
<times_loop> ::= times (<positive_int>) {<stmt_list>}
<dowhile_loop> ::= do {<stmt_list>} while (<conditional_expr>)
<update_expr> ::= <increment> | <decrement>
<increment> ::= <id>++
<decrement> ::= <id>--
<IO_stmt> ::= <input_stmt> | <output_stmt>
<input_stmt> ::= <single_input> | <multi_input>
<single_input> ::= READ <id> #READS INTO VARIABLE WITH <id>
<multi_input> ::= READ <input_list>
<input_list> ::= <id><id> | <id><input_list>
<output_stmt> ::= WRITE <expr> | WRITE <conditional_expr>
<IO_stmt_keyword> ::= READ | WRITE
#primitives
<primitive_func> ::= <inclination> | <altitude> | <temperature> | <acceleration> | <cam_control> | <timestamp> | <wifi> | <storage> | <battery> | <wait> | <array_size>
<inclination> ::= GET_INC
<altitude> ::= GET_ALT
<temperature> ::= GET_TEMP
<acceleration> ::= GET_ACC
<cam_control> ::= <cam_on> | <cam_off> | <snap_picture>
<cam_on> ::= CAM_ON
<cam_off> ::= CAM_OFF
<snap_picture> ::= SNAP_PIC
<timestamp> ::= GET_TIME
<wifi> ::= CONNECT_BASE
<storage> ::= GET_CAPACITY
<battery> ::= GET_BATTERY
<wait> ::= WAIT <id> | WAIT <positive_int>
<array_size> ::= GET_SIZE <positive_int><id>;
<primitive_keyword> ::= GET_INC | GET_ALT | GET_TEMP | GET_ACC | CAM_ON | CAM_OFF | SNAP_PIC | GET_TIME | CONNECT_BASE | GET_CAPACITY | GET_BATTERY | WAIT | GET_SIZE
<relational_op> ::= <= | >= | < | > | == | !=
<assignment_op> ::= = | += | -= | *= | /= |**= | %=
<id> ::= <normal_chars> | <normal_chars><id> | <id><digit>
<signed_int> ::= <positive_int> | <negative_int>
<positive_int> ::= <int> 
<negative_int> ::= -<int>
<int> ::= <digit> | <digit><int>
<float> ::= .<int> | <signed_int>.<int>
<bool> ::= TRUE | FALSE
<digit> ::= 0|1|2|3|4|5|6|7|8|9
<string> ::= "<string_exp>" | ""
<string_exp> :: <all_chars> | <all_chars><string> 
<char> ::= '<all_chars>'
<all_chars> ::= <normal_chars> | <special_chars> | <space> | <newline>
<normal_chars> ::= a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|x|y|z|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|_
<special_chars> ::= ! | @ | # | \$ | % | ^ | & | * | ( | ) | + | = | / | * | - | ; | ' | " | ; | '|' | { | } | [ | ]
<space> ::= ' '
<newline> ::= \n
<escape_char> ::= \\
<type> ::= <numeric_type> | BOL
<numeric_type> ::= <basic_numeric> | <floating_numeric> 
<basic_numeric> ::= INT | CHR | STR
<floating_numeric> ::= FLT 
<multi_array> ::= <array><array> | <array><multi_array>
<array> ::= [<array_exp>]
<array_exp> ::= <literal_without_array> | <literal_without_array>,<array_exp>
<keyword> ::= VOID | FUNC | RETURN | NULL | OR | AND | CONSTANT | <range_keyword> | <IO_stmt_keyword> | <primitive_keyword> | <bool> | <type>
